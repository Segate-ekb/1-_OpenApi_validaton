// BSLLS:DeprecatedMessage-off
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.СхемаOpenApi = Обработки.ПримерРаботыВалидатора.ПолучитьМакет("OpenApi").ПолучитьТекст();
	ЗагрузитьJsonДляПроверки();
	ЗаполнитьСписокВыбораИмяСхемПакета();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПереключательПриИзменении(Элемент)
	ЗагрузитьJsonДляПроверки();
КонецПроцедуры

&НаКлиенте
Процедура СхемаOpenApiПриИзменении(Элемент) // BSLLS:LatinAndCyrillicSymbolInWord-off
	ЗаполнитьСписокВыбораИмяСхемПакета();
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Валидировать(Команда)
	Если Не ЗначениеЗаполнено(ИмяСхемы) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Необходимо выбрать имя схемы!";
		Сообщение.ПутьКДанным = "ИмяСхемы";
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	ВалидироватьНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ВалидироватьНаСервере() // BSLLS:Typo-off
	МодельДанных = ВалидаторПакетовПовтИсп.JsonВОбъект(Объект.JsonДляПроверки);
	РезультатВалидации = ВалидаторПакетов.Валидировать(МодельДанных, ИмяСхемы, Объект.СхемаOpenApi);
	
	МассивПредупреждений = РезультатВалидации.Предупреждения;
	Для Каждого Предупреждение Из МассивПредупреждений Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Предупреждение;
		Сообщение.ПутьКДанным = "Объект.JsonДляПроверки";
		Сообщение.Сообщить();
	КонецЦикла;

	Если РезультатВалидации.Результат Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Валидация прошла успешно!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	МассивОшибок = РезультатВалидации.Ошибки;
	Для Каждого Ошибка Из МассивОшибок Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Ошибка;
		Сообщение.ПутьКДанным = "Объект.JsonДляПроверки";
		Сообщение.Сообщить();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьJsonДляПроверки() // BSLLS:LatinAndCyrillicSymbolInWord-off
Если Переключатель Тогда
	ИмяМакета = "НевалидныйJson";
Иначе
	ИмяМакета = "Валидныйjson";
КонецЕсли;
Объект.JsonДляПроверки = Обработки.ПримерРаботыВалидатора.ПолучитьМакет(ИмяМакета).ПолучитьТекст();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораИмяСхемПакета()
	МассивИмен = Новый Массив;
    Если ЗначениеЗаполнено(Объект.СхемаOpenApi) Тогда
        Схемы = ВалидаторПакетовПовтИсп.СхемыДанныхСпецификации(Объект.СхемаOpenApi);
        Для Каждого ЭлементСоответствия Из Схемы Цикл
            МассивИмен.Добавить(ЭлементСоответствия.Ключ);
        КонецЦикла;
        
    КонецЕсли;

    Элементы.ИмяСхемы.СписокВыбора.ЗагрузитьЗначения(МассивИмен);
КонецПроцедуры
#КонецОбласти
